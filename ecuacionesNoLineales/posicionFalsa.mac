/* [wxMaxima: input   start ] */
posicionFalsa(f,P0,P1,tol,N):=block([p0,p1,q0,q1],
    numer:true,
    /*valores iniciales*/
    p0:P0,
    p1:P1,
    q0:subst(p0,x,f),
    q1:subst(p1,x,f),
    i:2,
    bandera:0,
    
    /*imprimo valores iniciales en pantalla, las dos primeras iteraciones*/
    print("n=", 0,  "  P_n=",   p0,     "   f(P_n)="    ,q0),
    print(""),
    print("n=", 1,  "  P_n=",   p1, "   f(P_n)=",   q1),
    print(""),
    
    
    /*mientras no llegue al maximo de iteraciones y no 
    haya encontrado la solución*/
    while(i<=N and bandera=0)do 
    (
        p:p1-q1*(p1-p0)/(q1-q0), /*obtengo aproximacion p_n*/
        print("n=", i,  "  P_n=",   p,  "   f(P_n)=",   subst(p,x,f)),
        
        /*si es menor que la tolerancia*/
        if(abs(p-p1)<tol)then
        (
            bandera:1,
            print("El procedimiento fue exitoso tras ", i,  " iteraciones."),
            print("Aproximación: ", p)
        )
        else /*si no es menor que la tolerancia*/
        (
            i:i+1, 
            q:subst(p,x,f),
            
            if((signum(q)*signum(q1))<0)then
            (
                p0:p1,
                q0:q1
            ),
            
            p1:p,
            q1:q
        )
    
    ),
    
    if(bandera=0)then
    (
        print("El procedimiento no fue exitoso.")
    )
)$
/* [wxMaxima: input   end   ] */



